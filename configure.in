dnl Process this file with autoconf to produce a configure script.

dnl initialization

AC_PREREQ(2.13)

AC_INIT(gw/alt_charsets.h)
AC_CONFIG_HEADER(config.h)
AC_SUBST(SHELL)
AC_CONFIG_AUX_DIR(.)
dnl Gateway version number.

AC_DEFINE_UNQUOTED(VERSION, "`head -1 VERSION`")

dnl Target installation directory for documentation

AC_SUBST(docdir)
docdir='${prefix}/share/doc/kannel'

dnl Checks System Type.
AC_CANONICAL_HOST

case "$host" in
*-sun-solaris*) CFLAGS="$CFLAGS -DSunOS=1";;
esac

dnl Checks for programs.

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC
AC_PROG_LEX
AC_PATH_PROG(CONVERT, convert)
AC_PATH_PROG(PERL, perl)

dnl Checks for libraries.

AC_CHECK_LIB(m, log)
AC_CHECK_LIB(socket, accept)
AC_CHECK_LIB(nsl, inet_ntoa)
AC_CHECK_LIB(pthread, pthread_exit)
if test -z "$pthread"; then
	AC_CHECK_LIB(c_r, pthread_exit, [LIBS="$LIBS -lc_r"])
fi

AC_PATH_PROGS(XML_CONFIG, xml2-config xml-config, no)
if test "$XML_CONFIG" = "no"; then
	echo "You MUST have the gnome-xml library installed"; exit 1
else
        AC_MSG_CHECKING([libxml version])
	xml_version=`$XML_CONFIG --version`
	AC_MSG_RESULT([$xml_version])
	changequote(<<, >>)dnl
	case $xml_version in 
	*2.2.[5-9]*) ;;
	*2.2.[1-9][0-9]*) ;;
	*2.[3-9].[0-9]*) ;;
	*) AC_MSG_ERROR(<<Libxml is too old. You need at least 2.2.5>>) ;;
	esac
	changequote([, ])dnl
	LIBS="$LIBS `$XML_CONFIG --libs`"
	CFLAGS="$CFLAGS `$XML_CONFIG --cflags`"
fi

AC_CHECK_LIB(resolv, inet_ntop)
AC_CHECK_LIB(bind, inet_ntop)


dnl Check to see if we should do the WTLS thing, and which version to use

AC_ARG_WITH(wtls,
[  --with-wtls             select wtls version to use: openssl/baltimore], [
  case "$withval" in
  openssl) echo "OpenSSL WTLS selected, attempting to find libraries..."
	OLDLIBS="$LIBS"
	LIBS="$LIBS -L/usr/local/ssl/lib -lcrypto"
	AC_CHECK_LIB(crypto, RSA_new,
	[
		echo "OpenSSL found; WTLS enabled"
		CFLAGS="$CFLAGS -I/usr/local/ssl/include"
		AC_DEFINE(HAVE_WTLS_OPENSSL)
	],
	[
		LIBS=$OLDLIBS
		echo "OpenSSL not found; WTLS disabled"
	]
	)
	;;
  baltimore) echo "Baltimore WTLS not available" ;;
  *) echo "Unknown wtls version $withval. Oops."; exit 1 ;;
  esac
]
)


dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h sys/time.h sys/types.h unistd.h sys/poll.h)
AC_CHECK_HEADERS(pthread.h getopt.h syslog.h)

dnl Checks for typedefs, structures, and compiler characteristics.

AC_TRY_COMPILE(, [char *func = __FUNCTION__;], 
    	       AC_DEFINE(HAVE___FUNCTION__))
AC_TRY_COMPILE(, [char *func = __func__;], 
    	       AC_DEFINE(HAVE___FUNC__))


dnl Checks for library functions.

AC_CHECK_FUNCS(gettimeofday select socket strdup getopt_long)
AC_CHECK_FUNC(getopt, , LIBOBJS="$LIBOBJS utils/attgetopt.o")
AC_SUBST(LIBOBJS)

dnl Extra feature checks

dnl GW_HAVE_TYPE_FROM(HDRNAME, TYPE, HAVENAME, DESCRIPTION)
AC_DEFUN(GW_HAVE_TYPE_FROM, [
	AC_CACHE_CHECK([for $2 in <$1>], gw_cv_type_$3,
		AC_TRY_COMPILE([#ifdef HAVE_SYS_TYPES_H
#include <sys/types.h>
#endif
#include <$1>
], [$2 foo;],
			gw_cv_type_$3=yes, gw_cv_type_$3=no))
	if test $gw_cv_type_$3 = yes; then
		AC_DEFINE($3, 1, $4)
	fi
])

dnl GW_HAVE_FUNC_FROM(HDRNAME, FUNC, HAVENAME, DESCRIPTION)
AC_DEFUN(GW_HAVE_FUNC_FROM, [
	AC_CACHE_CHECK([for $2 in <$1>], gw_cv_func_$3,
		AC_TRY_COMPILE([#include <$1>], [void *foo = $2;],
			gw_cv_func_$3=yes, gw_cv_func_$3=no))
	if test $gw_cv_func_$3 = yes; then
		AC_DEFINE($3, 1, $4)
	fi
])

GW_HAVE_TYPE_FROM(sys/socket.h,
	socklen_t,
	HAVE_SOCKLEN_T,
	[Defined if there is a socklen_t in <sys/socket.h>])

GW_HAVE_FUNC_FROM(stdio.h, getopt, HAVE_GETOPT_IN_STDIO_H,
    	    	  [Does <stdio.h> declare getopt()?])
GW_HAVE_FUNC_FROM(unistd.h, getopt, HAVE_GETOPT_IN_UNISTD_H,
    	    	  [Does <unistd.h> declare getopt()?])

dnl Misfeature checks
AC_MSG_CHECKING(for working pthreads)
AC_TRY_RUN([#include <pthread.h>
#include <unistd.h>
int pid;
void testpid(void* foo);
int main(void){
    pthread_t child;
    pid=getpid();
    pthread_create(&child,NULL,(void*)testpid,NULL);
    pthread_join(child,NULL);
    return 0;
}
void testpid(void* foo){
    int mypid=getpid();
    if(mypid!=pid){
        /* Pthreads states that all threads should have the same PID
         * we dont!
         */
        exit(1);
    }else{
        exit(0);
    }
}
],echo yes , echo no ;CFLAGS="$CFLAGS -DBROKEN_PTHREADS=1", echo  Cross compiling - assuming they work)



dnl DocBook stuff

AC_CHECK_PROG(JADE, jade, jade, :)
AC_CHECK_PROG(JADETEX, jadetex, jadetex, :)
AC_CHECK_PROG(DVIPS, dvips, dvips, :)
AC_CHECK_PROG(FIG2DEV, fig2dev, fig2dev, :)
AC_CHECK_PROG(CONVERT, convert, convert, :)
AC_CHECK_PROG(PS2PDF, ps2pdf, ps2pdf, :)
AC_SUBST(HTML_DSL)
for file in /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh/html/docbook.dsl \
	/usr/lib/sgml/stylesheets/nwalsh-modular/html/docbook.dsl \
	/usr/share/sgml/docbook/dsssl-stylesheets-1.59/html/docbook.dsl \
	/usr/share/sgml/docbook/dsssl-stylesheets/html/docbook.dsl
do
	AC_CHECK_FILE($file,HTML_DSL=$file)
done
AC_SUBST(TEX_DSL)
for file in /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh/print/docbook.dsl \
	/usr/lib/sgml/stylesheets/nwalsh-modular/print/docbook.dsl \
	/usr/share/sgml/docbook/dsssl-stylesheets-1.59/print/docbook.dsl \
	/usr/share/sgml/docbook/dsssl-stylesheets/print/docbook.dsl
do
	AC_CHECK_FILE($file,TEX_DSL=$file)
done


dnl Implement --with-defaults option.

AC_ARG_WITH(defaults,
[  --with-defaults         set default configure options: speed/debug], [
  case "$withval" in
  speed) assertiondefault=no
         mallocdefault=native
         ;;
  debug) assertiondefault=yes
         mallocdefault=check
         ;;
  *) echo "unknown --with-defaults parameter $withval"
     exit 1
     ;;
  esac
], [
  dnl Same as debug
  assertiondefault=yes
  mallocdefault=check
])


dnl Implement --enable-docs option.

AC_SUBST(DOCSTARGET)
if test "x$HTML_DSL" = "x" -o "x$TEX_DSL" = "x"
then
	DOCSTARGET="no-docs"
else
	DOCSTARGET="docs"
fi

AC_ARG_ENABLE(docs,
[  --enable-docs           enable building of documentation (default: enabled)], [
  if test "$enableval" = "yes"
  then
      DOCSTARGET="docs"
  else
      DOCSTARGET="no-docs"
  fi
])
case "$DOCSTARGET" in
no-docs) echo Not building documentation. ;;
docs) echo Documentation will be built as well. ;;
esac


dnl Implement the --with-mysql option. This will set HAVE_MYSQL in config.h
dnl accordingly and enable the usage of the libmysqlclient routines.

AC_ARG_WITH(mysql,
[  --with-mysql[=DIR]      where to look for MySQL libs and header files
                          DIR points to the installation [/usr/local]],
[AC_MSG_CHECKING(for MySQL client support in)
 if test "$withval" = "yes"; then
   loc="/usr/local"
 else
   loc="$withval"
 fi
 AC_MSG_RESULT($loc)
 AC_CHECK_FILE("$loc/include/mysql/mysql.h", 
   [CFLAGS="$CFLAGS -I$loc/include/mysql"; LIBS="$LIBS -L$loc/lib/mysql"],
   [AC_CHECK_FILE("$loc/include/mysql.h", 
     [CFLAGS="$CFLAGS -I$loc/include"; LIBS="$LIBS -L$loc/lib"],
     [AC_MSG_ERROR(Unable to find mysql.h under $loc)]
    )]
 )
 AC_CHECK_HEADERS(mysql/mysql.h mysql/mysql_com.h mysql/mysql_version.h)
 AC_CHECK_LIB(mysqlclient, mysql_init, [LIBS="$LIBS -lmysqlclient"]) 
 AC_DEFINE(HAVE_MYSQL)
 MYSQL="yes"
])


dnl Implement the --enable-mysql-dlr option. 
AC_ARG_ENABLE(mysql-dlr,
[  --enable-mysql-dlr      enable MySQL support for DLR (default: disabled)], 
[if test "$enableval" = "yes"; then
   if test "$MYSQL" != "yes"; then
     AC_MSG_ERROR(Option --enable-mysql-dlr requires MySQL client support)
   else
     echo enabling MySQL support for DLR
   fi
   AC_DEFINE(MYSQL_DLR)
 fi
])


dnl Implement the --enable-pam option.

AC_ARG_ENABLE(pam,
[  --enable-pam	          enable pam authentication (default: disabled)], [
    if test "$enableval" = "yes"
    then
	AC_CHECK_LIB(pam, pam_end)
	AC_CHECK_LIB(dl,main)
	AC_CHECK_HEADERS(security/pam_appl.h)
	PAMTARGET="pam"
    else
	PAMTARGET="no-pam"
    fi
])
case "$PAMTARGET" in
no-pam) echo pam authentication is disabled. ;;
pam) echo pam authentication is enabled. ;;
esac


dnl Implement --enable-debug option.

AC_ARG_ENABLE(debug,
[  --enable-debug	  enable non-reentrant debugging for wmls compiler (default: disabled)], [
  echo enabling WMLScript compiler debugging
  if test -n "$GCC"; then
    CFLAGS="$CFLAGS -Wall"
  fi
  AC_DEFINE(WS_DEBUG)
])


dnl Implement --enable-localtime option.

AC_ARG_ENABLE(localtime,
[  --enable-localtime	  log file time stamps in local time, not GMT (default: disabled)], [
  if test "$enableval" = yes; then
      echo enabling local time
      AC_DEFINE(LOG_TIMESTAMP_LOCALTIME)
  fi
])


dnl Implement --disable-assertions option.


AC_ARG_ENABLE(assertions,
[  --disable-assertions    turn off assertion checking], [
  if test "$enableval" = "no"
  then
    echo disabling assertion checking
    AC_DEFINE(NO_GWASSERT)
  fi
], [
  if test "$assertiondefault" = "no"
  then
    echo disabling assertion checking
    AC_DEFINE(NO_GWASSERT)
  fi
])


dnl --with-malloc=native/fast/checking/slow

AC_ARG_WITH(malloc,
[  --with-malloc           select malloc wrapper to use: native/checking/slow], [
  case "$withval" in
  native) AC_DEFINE(USE_GWMEM_NATIVE)
          echo using native malloc
          ;;
  check) AC_DEFINE(USE_GWMEM_CHECK)
         echo using checking malloc
         ;;
  slow) AC_DEFINE(USE_GWMEM_SLOW)
        echo using slow malloc
	;;
  *) echo "Unknown malloc wrapper $withval. Oops."; exit 1 ;;
  esac
], [
  case "$mallocdefault" in
  native) AC_DEFINE(USE_GWMEM_NATIVE)
          echo using native malloc
          ;;
  slow) AC_DEFINE(USE_GWMEM_SLOW)
        echo using slow malloc
	;;
  *) AC_DEFINE(USE_GWMEM_CHECK)
     echo using checking malloc
     ;;
  esac
])


dnl --enable-mutex-stats option.

AC_ARG_ENABLE(mutex-stats,
[  --enable-mutex-stats    produce information about lock contention], [
  if test "$enableval" = yes; then
      AC_DEFINE(MUTEX_STATS)
  fi
])


dnl --enable-start-stop-daemon option.

AC_ARG_ENABLE(start-stop-daemon,
[  --enable-start-stop-daemon  compile the start-stop-daemon program], [
  if test "$enableval" = yes; then
     STARTSTOPDAEMONSRC="utils/start-stop-daemon.c"
  fi
])
AC_SUBST(STARTSTOPDAEMONSRC)


dnl --disable-ssl option.

SSL="ssl"

AC_ARG_ENABLE(ssl,
[  --enable-ssl  enable support for ssl and https (default: enabled)], [
  if test "$enableval" = no ; then
    echo Disabling SSL support.
	  SSL="no-ssl";
	fi
])

if test "x$SSL" = "xssl" ; then
  echo Attempting to compile with SSL support.
  AC_CHECK_LIB(ssl, SSL_library_init,
  [ AC_MSG_CHECKING(whether the OpenSSL library is multithread-enabled)
    AC_TRY_RUN([ #define OPENSSL_THREAD_DEFINES
                 #include <openssl/opensslconf.h>
                 int main(void) {
                 #if defined(THREADS)
                  exit(0);
                 #else
                  exit(1);
                 #endif
                 }
              ], echo yes;AC_DEFINE(HAVE_LIBSSL)
              LIBS="$LIBS -lssl",
              echo no;echo Either get a multithread-enabled SSL or configure with --disable-ssl;
              exit 1,
              echo "Cross-compiling; make sure your SSL library is multithread-enabled"
              )
  ])
fi

dnl Final Output

AC_OUTPUT(Makefile)
